plugins {
    alias libs.plugins.fabric.loom
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "ind": "    "])
        }
    }
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
    validateAccessWidener {
        enabled = false
    }
    accessWidenerPath = file("src/main/resources/nnbsp-fix.accesswidener")
}

dependencies {
    minecraft libs.minecraft
    mappings variantOf(libs.yarn.mappings) { classifier "" }
    modImplementation libs.fabric.loader
    vineflowerDecompilerClasspath libs.vineflower
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(20)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_20
}

jar {
    from("LICENSE")
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
